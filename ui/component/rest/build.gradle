apply plugin: "groovy"
apply plugin: "cz.habarta.typescript-generator"

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "cz.habarta.typescript-generator:typescript-generator-gradle-plugin:$typescriptGeneratorVersion"
    }
}

dependencies {
    compileOnly resolveDependency(":model-util")
    implementation findProject(":model")
    implementation findProject(":agent")
    implementation "cz.habarta.typescript-generator:typescript-generator-core:$typescriptGeneratorVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion"
}

// This seems hacky but it is the only way to get the closure from the method call
project.task(Collections.singletonMap(Task.TASK_TYPE, cz.habarta.typescript.generator.gradle.GenerateTask), "generateTypescriptModelInfo")
generateTypescriptModelInfo createTSGeneratorConfigForModel("$buildDir/model.ts", findProject(":model"))

generateTypeScript createTSGeneratorConfigForClient("src/restclient.ts", findProject(":model"))
generateTypeScript.dependsOn(generateTypescriptModelInfo)

clean {
    doLast {
        def dir = new File("${projectDir}/dist")
        dir.deleteDir()
    }
}

build.dependsOn generateTypeScript, npmBuild
npmBuild.dependsOn generateTypeScript

task prepareUi() {
    dependsOn clean, build, npmPrepare
}

task publishUi() {
    dependsOn clean, build, npmPublish
}
