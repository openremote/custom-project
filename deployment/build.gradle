apply plugin: "java-library"

dependencies {
    api project(":setup")
}

tasks.register('license') {
    doLast {
        def licenseFiles = new ArrayList<>()
        licenseFiles.add("${rootDir}/LICENSE.txt")

        def toConcatenate = files(licenseFiles.toArray())
        def outputFileName = "${buildDir}/image/manager/app/LICENSE.txt"
        def output = new File(outputFileName)
        if (output.exists()) {
            output.delete()
        }
        output.getParentFile().mkdirs()
        output.createNewFile()
        output.write('') // truncate output if needed
        toConcatenate.each { f -> output << f.text }
    }
}

tasks.register('installDist', Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(parent.getTasksByName('installDist', true).findAll {
        // Don't create circular dependency or depend on built in openremote submodule apps
        it.project != project && !it.project.path.startsWith(":openremote:ui:app")
    })

    into("$buildDir")

    from "Dockerfile"

    into("image") {
        from projectDir
        exclude "build.gradle", "Dockerfile", "build", "**/*.mbtiles", "src", "**/*.md", ".gitignore"
    }

    into("image/manager/extensions") {
        from getDeploymentJars()
    }
}
