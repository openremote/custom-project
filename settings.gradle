pluginManagement {
    plugins {
        id "com.cherryperry.gradle-file-encrypt" version "2.0.0"
        id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.3"
        id 'cz.habarta.typescript-generator' version "$typescriptGeneratorVersion"
    }
}

plugins {
    id "com.cherryperry.gradle-file-encrypt" apply false
    id "org.jetbrains.gradle.plugin.idea-ext" apply false
    id "cz.habarta.typescript-generator" apply false
}

rootProject.name = "$projectName"

// Include sub-projects dynamically, every directory with a build.gradle (and no .buildignore)
fileTree(dir: rootDir, include: "**/build.gradle", excludes: ["**/node_modules/**", "**/generic_app/**"])
        .filter { it.parent != rootDir }
        .filter { !file("${it.parent}/.buildignore").exists() }
        .each {
    include it.parent.replace(rootDir.canonicalPath, "").replace("\\", ":").replace("/", ":")
}
